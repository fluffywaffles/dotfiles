#
# Path!
# Djikstra! ... would be confused to know he is referenced here.
#

# Make sure local bins come _before_ cargo bins, without repeating it
# $array[(i)...] is index-of; if not found, returns $((${#array} + 1))
local_bin_index=$path[(i)$HOME/.local/bin]
if [[ !($local_bin_index -ge ${#path}) ]]; then
  export path=(
    # Everything up to and including $HOME/.local/bin
    $path[0,$local_bin_index]
    $HOME/.cargo/bin
    # Everything after $HOME/.local/bin, to the end (-1)
    $path[$local_bin_index+1,-1]
  )
else
  export path=($HOME/.cargo/bin $path)
fi

#
# Keyboard
# QWERT
#

# Go it, fastly!
export KEYTIMEOUT=1

# Vi keybindings
bindkey -v

# Keybindings that should be preserved even after entering and exiting vi normal mode.
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word

# Give me extended glob support
setopt extendedglob

# Don't share history between zsh sessions
setopt no_sharehistory

# ibus
# export GTK_IM_MODULE=ibus  # youbus
# export XMODIFIERS=@im=ibus # webus
# export QT_IM_MODULE=ibus   # bus is life

#
# Aliases, functions, source-able software (plugins), ...?
#

# nvim pls
export EDITOR="nvim"

# Aliaseses
source ~/.aliases

# Better FZF (using ag for filtering)
export FZF_DEFAULT_COMMAND='ag -g ""'

# Load z
source $HOME/.local/zsh-functions/z/z.sh

#
# ZLE (zsh line editor)
#

# NOTE: which characters should be removed during completion? (not pipe)
ZLE_REMOVE_SUFFIX_CHARS=$' \t\n;&'

# NOTE: without this, there's space after the RPROMPT
# NOTE: this can only be disabled within tmux
if [[ "$TERM" == "screen-256olor" ]]; then
  ZLE_RPROMPT_INDENT=0
else
  ZLE_RPROMPT_INDENT=1
fi

#
# Da PrompT
#

# Only do all this if we're in a decent terminal...
if [[ $TERM != "linux" ]]; then
  # Shorten the prompt
  # Stolen (!) from the "norm" zsh theme in OMZ
  printf -v PROMPT "%s %s %s%s"         \
    '%{$fg[yellow]%}▌ƒ▐'                \
    '%{$fg[green]%}%c'                  \
    '%{$fg[yellow]%}$(git_prompt_info)' \
    '%{$reset_color%}'
  ZSH_THEME_GIT_PROMPT_PREFIX="∙ %{$fg[red]%}"
  ZSH_THEME_GIT_PROMPT_SUFFIX=" "
else
  printf -v PROMPT "%s %s %s%s"         \
    '%{$fg[yellow]%}f'                  \
    '%{$fg[green]%}%c'                  \
    '%{$fg[yellow]%}$(git_prompt_info)' \
    '%{$reset_color%}'
  ZSH_THEME_GIT_PROMPT_PREFIX=". %{$fg[red]%}"
  ZSH_THEME_GIT_PROMPT_SUFFIX=" "
fi

#
# RPrompT (the one on the right!)
# Print battery visual indicator and vim-mode on right-hand side of prompt
#

VIM_NORMAL_MODE="%{$FX[bold]$fg[blue]%}[nrm]%{$reset_color%}"
VIM_INSERT_MODE="%{$FX[bold]$fg[grey]%}[ins]%{$reset_color%}"

# Load batpower (for battery strength indicators in RPROMPT)
source $HOME/.local/zsh-functions/batpower.zsh

# Draw at start of each line print, and when keymap changes
function update-rprompt {
  printf -v RPROMPT '%s%s$EPS1'                                         \
    "${${KEYMAP/vicmd/$VIM_NORMAL_MODE}/(main|viins)/$VIM_INSERT_MODE}" \
    "${batpower_enabled:+ }$(batpower-visual-battery)"
}

# Widget functions (for when zsh redraws the prompt)
function zle-line-init zle-keymap-select {
  update-rprompt
  zle reset-prompt
}

# Register the widget functions
zle -N zle-line-init
zle -N zle-keymap-select

# Set up batpower and rprompt!
batpower-setup >/dev/null
update-rprompt

# Make ls colors consistent
export LS_COLORS="rs=0:di=01;34:ln=01;36:mh=00:pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:or=40;31;01:mi=00:su=37;41:sg=30;43:ca=30;41:tw=30;42:ow=34;42:st=37;44:ex=01;32:*.tar=01;31:*.tgz=01;31:*.arc=01;31:*.arj=01;31:*.taz=01;31:*.lha=01;31:*.lz4=01;31:*.lzh=01;31:*.lzma=01;31:*.tlz=01;31:*.txz=01;31:*.tzo=01;31:*.t7z=01;31:*.zip=01;31:*.z=01;31:*.dz=01;31:*.gz=01;31:*.lrz=01;31:*.lz=0"
