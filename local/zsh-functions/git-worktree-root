#!/usr/bin/env -S zsh -euo pipefail

function git-worktree-root() {
  local worktree_list main_worktree commitish worktree_root_link
  local match mbegin mend # zsh regexp match variables set by =~

  # formatted error printing, only if [-]-v[erbose]
  local verbose=${1}
  function eprintf {
    >&2 printf ${@}
  }

  # exit if not in a git directory
  local git_root=$(git rev-parse --show-toplevel 2>/dev/null)
  if ! git rev-parse --git-dir &>/dev/null; then return 0; fi

  # get the main worktree, which is the first result in the list
  git worktree list --porcelain | { read main_worktree; read commitish }
  # regex parse the worktree path
  [[ "${main_worktree}" =~ worktree\ ([^$'\n']+) ]]

  # error if the main worktree path is NOT a bare repository
  if ! [[ ${commitish} =~ ^bare$ ]]; then
    if [[ ${verbose} =~ ^-+v(erbose|)$ ]]; then
      eprintf '!! ─ The main worktree is not a separate bare repository\n'
    fi
    return 1
  fi

  # now we know where our bare root is
  worktree_root_link="${match}/worktree-root"
  if ! [[ -L "${worktree_root_link}" ]]; then
    eprintf '!! ─ The bare worktree root has no `worktree-root` symlink\n'
    exit 1
  fi

  printf '%s\n' $(readlink -e ${worktree_root_link})
}
